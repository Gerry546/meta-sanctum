#@TYPE: Machine
#@NAME: Reterminal machine based on a Raspberry Pi CM4
#@DESCRIPTION: Machine configuration for running a reterminal

MACHINEOVERRIDES = "raspberrypi4:raspberrypi4-64:${MACHINE}"

require conf/machine/raspberrypi4-64.conf

# Machine settings
#########################################

MACHINE_FEATURES:append = "\
    bluetooth \
    rtc \
    screen \
    serial \
    touchscreen \
    usbgadget \
    usbhost \
    vfat \
    wifi \
"

IMAGE_FSTYPES = " \
    wic \
    wic.bz2 \
    wic.bmap \
    ext4 \
"

###########################################################################
# As it is a raspberry pi product we need to enable the configuration and
# ensure that the overlay is loaded

RPI_KERNEL_DEVICETREE = " \
    broadcom/bcm2711-rpi-cm4.dtb \ 
"

# Enable UART communication
ENABLE_UART = "1"

# Enable the USB ports at the side
ENABLE_DWC2_HOST = "1"

# I2C1 is used for:
#   Accelerometer       (0x19)
#   Light sensor        (0x29)
#   IO Expander         (0x38)
#   Display             (0x45)
# I2C3 is used for:
#   RTC                 (0x51)
#   Encryption sensor
ENABLE_I2C = "1"
ENABLE_I2C3 = "1"
RPI_KERNEL_DEVICETREE_OVERLAYS:append = " \
    overlays/i2c3.dtbo \
"

# Add the overlay which contains the specifics for the reterminal
ENABLE_RETERMINAL = "1"
RPI_KERNEL_DEVICETREE_OVERLAYS:append = " \
    overlays/reterminal.dtbo \
"

# # SPI is used 
# #   40 pins I/O connector
# ENABLE_SPI_BUS = "1"


# Bootloader settings
#########################################
# Default for the distro to U-Boot
RPI_USE_U_BOOT = "1"

RPI_EXTRA_IMAGE_BOOT_FILES += "\
    fitImage \
"

do_image_wic[depends] += " \
    reterminal-fitimage:do_deploy \
"

RPI_EXTRA_IMAGE_BOOT_FILES:remove = "\
    ${KERNEL_IMAGETYPE} \
"

KERNEL_BOOTCMD = "bootm"

# WIC Settings
#########################################
WKS_FILES = "reterminal.wks"